/** HerpDerpCraft build script, based off of Forge 1.10.2's template */

// Gradle dependencies for this build script
buildscript
{
    repositories
    {
        jcenter()

        maven
        {
            name = "forge"
            url  = "http://files.minecraftforge.net/maven"
        }
    }

    dependencies
    {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
    }
}

// Apply the ForgeGradle plugin
apply plugin: 'net.minecraftforge.gradle.forge'

// Gradle project properties
version = "1.10.2-0.0.1"
group   = "roycurtis.HerpDerp"

// Specifies the output jar file's prefix
archivesBaseName = "HerpDerp"

// Sets the output directory of the mod's jar file
jar.destinationDir = file './output'

// Enforces use of Java 1.8 language level
compileJava
{
    sourceCompatibility = "1.8"
    targetCompatibility = "1.8"
}

// Minecraft project properties
minecraft
{
    version  = "1.10.2-12.18.1.2011"
    // Specifies the working directory for ForgeGradle run configs
    runDir   = "run"
    // Which MCP decompiled Minecraft symbol mappings to use
    mappings = "snapshot_20160711"
}

// Fixes issues with Unicode characters in source files
// From https://discuss.gradle.org/t/special-characters-in-java-string-literals-arent-interpreted-correctly/4665
tasks.withType(JavaCompile)
{
    options.encoding = "UTF-8"
}

processResources
{
    inputs.property "modid",     project.archivesBaseName
    inputs.property "version",   project.version
    inputs.property "mcversion", project.minecraft.version

    // Only replace properties in mcmod.info
    from(sourceSets.main.resources.srcDirs)
    {
        include 'mcmod.info'

        // Replace version and mcversion
        expand (
            'modid':     project.archivesBaseName,
            'version':   project.version,
            'mcversion': project.minecraft.version
        )
    }

    from(sourceSets.main.resources.srcDirs)
    {
        exclude 'mcmod.info'
    }
}